
<script type="x-shader/x-vertex" id="toonPhong-vshader-source">
    attribute vec3 a_coords;
    attribute vec3 a_normal;
    uniform mat4 modelview;
    uniform mat4 projection;
    uniform vec4 lightPosition;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;
    varying vec4 v_lightPosition;

    void main() {
        vec4 coords = vec4(a_coords,1.0);
        vec4 eyeCoords = modelview * coords;
        gl_Position = projection * eyeCoords;
        v_normal = normalize(a_normal);
        v_eyeCoords = eyeCoords.xyz/eyeCoords.w; 
        v_lightPosition = modelview * lightPosition;
    }
</script>

<script type="x-shader/x-fragment" id="toonPhong-fshader-source">
    #ifdef GL_FRAGMENT_PRECISION_HIGH
         precision highp float;
    #else
         precision mediump float;
    #endif
    uniform mat3 normalMatrix;
    uniform vec4 diffuseColor;
    uniform vec3 specularColor;
    uniform float specularExponent;
    uniform float ambientLighting;
    uniform float factor;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;
    varying vec4 v_lightPosition;
    void main() {
        vec3 N, L, R, V;  // vectors for lighting equation
        N = normalize( normalMatrix*v_normal );
        if ( v_lightPosition.w == 0.0 ) {
            L = normalize( v_lightPosition.xyz );
        }
        else {
            L = normalize( v_lightPosition.xyz/v_lightPosition.w - v_eyeCoords );
        }
        R = -reflect(L,N);
        V = normalize( -v_eyeCoords);  

        float dotLN = dot(L,N);
        float shading = (dotLN+1.0)/2.0;
        float step = 1.0/3.0;
        float stepA = pow(step*2.0, factor);
        float stepB = pow(step, factor);
        if(shading > stepA) {
            shading = 0.65 + 0.35*(shading-stepA)/(1.0-stepA);
        } else if(shading > stepB) {
            shading = 0.5;
        } else {
            shading = 0.0;
        }

        vec3 color = (ambientLighting + (1.0-ambientLighting)*shading) * diffuseColor.rgb;
        gl_FragColor = vec4(color, diffuseColor.a);
    }
</script>