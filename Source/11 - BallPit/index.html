<!DOCTYPE html>
<meta charset="UTF-8">
<html>
<head>
<title>TestGL</title>
<style>
    body {
        background-color: #EEEEEE;
    }
    label {
        white-space: pre;
    }
    body {
      background-color:black;
    }
    .slidecontainer {
      width: 100%; /* Width of the outside container */
    }

    /* The slider itself */
    .slider {
      -webkit-appearance: none;  /* Override default CSS styles */
      appearance: none;
      width: 50%; /* Full-width */
      height: 15px; /* Specified height */
      background: #d3d3d3; /* Grey background */
      outline: none; /* Remove outline */
      opacity: 0.7; /* Set transparency (for mouse-over effects on hover) */
      -webkit-transition: .2s; /* 0.2 seconds transition on hover */
      transition: opacity .2s;
    }

    /* Mouse-over effects */
    .slider:hover {
      opacity: 1; 
    }
</style>

<script type="x-shader/x-vertex" id="phongTexture-vshader-source">
   attribute vec3 a_coords;
    attribute vec2 a_texcoord;
    attribute vec3 a_normal;

    uniform mat4 modelview;
    uniform mat4 projection; 
    varying vec3 v_normal;
    varying vec3 v_eyeCoords; 
    varying vec2 v_texcoord;

    void main() {
      vec4 coords = vec4(a_coords, 1.0);
      vec4 eyeCoords = modelview * coords;
      gl_Position = projection * eyeCoords;

      v_normal = a_normal;
      v_texcoord = a_texcoord;
      v_eyeCoords = eyeCoords.xyz/eyeCoords.w; // (Note: eyeCoords.w is 1 unless modelview is weird) 
    } 
</script>

<script type="x-shader/x-fragment" id="phongTexture-fshader-source">
  #ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
  #else
    precision mediump float;
  #endif
  uniform mat3 normalMatrix; 
  uniform sampler2D u_texture;
  uniform vec4 lightPosition;
  uniform vec3 specularColor;
  uniform float specularExponent; 
  varying vec2 v_texcoord;
  varying vec3 v_normal;
  varying vec3 v_eyeCoords;

  void main() {
    vec3 N, L, R, V;  // vectors for lighting equation
    N = normalize( normalMatrix*v_normal );
    if ( lightPosition.w == 0.0 ) {
        L = normalize( lightPosition.xyz );
    } else {
        L = normalize( lightPosition.xyz/lightPosition.w - v_eyeCoords );
    }
    R = -reflect(L,N);
    V = normalize( -v_eyeCoords);  // (Assumes a perspective projection.)
    if ( dot(L,N) <= 0.0 ) {
        gl_FragColor = vec4(0,0,0,1);
    } else {
        vec4 sample = texture2D(u_texture, v_texcoord);
        vec3 color = 0.8*dot(L,N) * sample.rgb;

        if (dot(R,V) > 0.0) {
            color += 0.4*pow(dot(R,V),specularExponent) * specularColor;
        } 
        gl_FragColor = vec4(color, 1);
    } 
  }    
</script>

<script type="x-shader/x-vertex" id="phongColor-vshader-source">
    attribute vec3 a_coords;
    attribute vec3 a_normal;
    uniform mat4 modelview;
    uniform mat4 projection;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;
    void main() {
        vec4 coords = vec4(a_coords,1.0);
        vec4 eyeCoords = modelview * coords;
        gl_Position = projection * eyeCoords;
        v_normal = normalize(a_normal);
        v_eyeCoords = eyeCoords.xyz/eyeCoords.w; 
    }
</script>

<script type="x-shader/x-fragment" id="phongColor-fshader-source">
    #ifdef GL_FRAGMENT_PRECISION_HIGH
       precision highp float;
    #else
       precision mediump float;
    #endif
    uniform mat3 normalMatrix;
    uniform vec4 lightPosition;
    uniform vec4 diffuseColor;
    uniform vec3 specularColor;
    uniform float specularExponent;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;
    void main() {
        vec3 N, L, R, V;  // vectors for lighting equation
        N = normalize( normalMatrix*v_normal );
        if ( lightPosition.w == 0.0 ) {
            L = normalize( lightPosition.xyz );
        }
        else {
            L = normalize( lightPosition.xyz/lightPosition.w - v_eyeCoords );
        }
        R = -reflect(L,N);
        V = normalize( -v_eyeCoords);  // (Assumes a perspective projection.)
        if ( dot(L,N) <= 0.0 && false ) {
            gl_FragColor = vec4(0,0,0,1);
        }
        else {
            //vec3 color = 0.8*dot(L,N) * diffuseColor.rgb;
            vec3 color = diffuseColor.rgb;
            if (dot(R,V) > 0.0) {
                color += 0.4*pow(dot(R,V),specularExponent) * specularColor;
            }
            gl_FragColor = vec4(color, diffuseColor.a);
        }
    }
</script>

<script type="x-shader/x-vertex" id="unlitTexture-vshader-source">
  attribute vec3 a_coords;
  attribute vec2 a_texcoord;
  uniform mat4 modelview;
  uniform mat4 projection; 
  varying vec2 v_texcoord;

  void main() {
    vec4 coords = vec4(a_coords, 1.0);
    vec4 eyeCoords = modelview * coords;
    gl_Position = projection * eyeCoords;
    v_texcoord = a_texcoord;
  } 

</script>

<script type="x-shader/x-fragment" id="unlitTexture-fshader-source">
  #ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
  #else
    precision mediump float;
  #endif
  uniform sampler2D u_texture;
  varying vec2 v_texcoord;

  void main() {
    gl_FragColor = texture2D(u_texture, v_texcoord);
  }    
</script>


<script type="x-shader/x-vertex" id="unlitUniformColor-vshader-source">
  attribute vec3 a_coords;
  uniform mat4 modelview;
  uniform mat4 projection; 

  void main() {
      vec4 coords = vec4(a_coords, 1.0);
      vec4 eyeCoords = modelview * coords;
      gl_Position = projection * eyeCoords;
  } 
</script>

<script type="x-shader/x-fragment" id="unlitUniformColor-fshader-source">
  #ifdef GL_FRAGMENT_PRECISION_HIGH
      precision highp float;
  #else
      precision mediump float;
  #endif
  uniform vec3 color;

  void main() {
      gl_FragColor = vec4(color, 1.0);
  }       
</script>
<script type="x-shader/x-vertex" id="unlitPoint-vshader-source">
  attribute vec3 a_coords;
  attribute vec3 a_color;
  attribute float a_size;
  varying vec3 v_color;
  uniform mat4 modelview;
  uniform mat4 projection; 
  void main() {
    gl_PointSize = a_size; 
    vec4 coords = vec4(a_coords, 1.0);
    vec4 eyeCoords = modelview * coords;
    gl_Position = projection * eyeCoords;
    v_color = a_color;
  } 
</script>

<script type="x-shader/x-fragment" id="unlitPoint-fshader-source">
  #ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
  #else
    precision mediump float;
  #endif
  varying vec3 v_color;
  void main() {
    gl_FragColor = vec4(v_color, 1.0);
  }     
</script>

<script type="x-shader/x-vertex" id="cubemapReflection-vshader-source">
     uniform mat4 projection;
     uniform mat4 modelview;
     attribute vec3 a_coords;
     attribute vec3 a_normal;
     varying vec3 v_eyeCoords;
     varying vec3 v_normal;
     void main() {
        vec4 eyeCoords = modelview * vec4(a_coords,1.0);
        gl_Position = projection * eyeCoords;
        v_eyeCoords = eyeCoords.xyz;
        v_normal = normalize(a_normal);
     }
</script>

<script type="x-shader/x-fragment" id="cubemapReflection-fshader-source">
     precision mediump float;
     varying vec3 v_normal;
     varying vec3 v_eyeCoords;
     uniform samplerCube skybox;
     uniform mat3 normalMatrix;
     uniform mat3 inverseViewTransform;
     void main() {
          vec3 N = normalize(normalMatrix * v_normal);
          vec3 V = -v_eyeCoords;
          vec3 R = -reflect(V,N);
          vec3 T = inverseViewTransform * R; // Transform by inverse of the view transform that was applied to the skybox
          gl_FragColor = vec4(0.1, 0.1, 0.1, 0) + textureCube(skybox, T);
          //gl_FragColor = vec4(0,0,1,1);
     }
</script>

<script type="x-shader/x-vertex" id="skybox-vshader-source">
     uniform mat4 projection;
     uniform mat4 modelview;
     attribute vec3 a_coords;
     varying vec3 v_coords;
     void main() {
        vec4 eyeCoords = modelview * vec4(a_coords,1.0);
        gl_Position = projection * eyeCoords;
        v_coords = a_coords;
     }
</script>

<script type="x-shader/x-fragment" id="skybox-fshader-source">
     precision mediump float;
     varying vec3 v_coords;
     uniform samplerCube skybox;
     void main() {
          gl_FragColor = textureCube(skybox, v_coords);
     }
</script>
<script src="../base/gl-matrix-min.js"></script>
<script src="../base/trackball-rotator.js"></script>
<script src="../base/zoomer.js"></script>
<script src="../base/basic-object-models-IFS.js"></script>
<script src="../base/noise.js"></script>
<script src="../base/fnc.js"></script>
<script src="../base/gl-base.js"></script>
<script src="index.js"></script>

<style>
</style>
</head>

<body onload="init()">

<div style="display:flex">
	<div id="canvas-holder">
	   <canvas width=800 height=800 id="webglcanvas" style="background-color:red"></canvas>
	</div>
</div>

</body>
</html>

